# importing file operations
open := $$stdio\fopen
close := $$stdio\fclose
write_char := $$stdio\fputc
write_str := $$stdio\fputs

# local import
int_to_str := $int_to_str

# colour
min := (a, b) {a < b -> a; b}
max := (a, b) {a > b -> a; b}
rgb := (r: r, g: g, b: b) {
    r: max(0, min(r, 255)),
    g: max(0, min(g, 255)),
    b: max(0, min(b, 255))
}

# shader type: (x: int, y: int) {r: int, g: int, b: int}
ppm_image := (filename, width, height, shader) {
    file := open(&filename, &"wb\0")

    # header
    _ := write_str(&"P6\0", file)
    _ := write_char(32, file)
    _ := write_str(&int_to_str(width), file)
    _ := write_char(32, file)
    _ := write_str(&int_to_str(height), file)
    _ := write_char(32, file)
    _ := write_str(&"255\0", file)
    _ := write_char(32, file)

    # pixel values
    _ := (x, y, file) {
        y = height -> {}
        x = width -> @(0, y + 1, file)

        colour := rgb(shader(x, y))
        _ := write_char(colour.r, file)
        _ := write_char(colour.g, file)
        _ := write_char(colour.b, file)

        @(x + 1, y, file)
    } (0, 0, file)

    _ := close(file)
    "done"
}

ppm_image
